plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'pl.tenfajnybartek'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    // ðŸš¨ NOWE: Repozytorium dla LuckPerms
    maven { url 'https://repo.minebench.de/' }
    // ðŸš¨ NOWE: Repozytorium Sonatype (czÄ™sto potrzebne dla Vault)
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'mysql:mysql-connector-java:8.0.33'
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly 'net.luckperms:api:5.4' // LuckPerms API
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.zaxxer.hikari', 'pl.tenfajnybartek.libs.hikari'
    relocate 'com.mysql', 'pl.tenfajnybartek.libs.mysql'
    minimize()
}

tasks.build.dependsOn tasks.shadowJar

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

// WRACAMY DO 21
def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}